apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }
tasks.withType(GroovyCompile) { options.encoding = 'UTF-8' }

group = 'com.blackducksoftware.integration'
version = '0.2.3-SNAPSHOT'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    mavenCentral()
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment ->
                signing.signPom(deployment)
            }
            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name rootProject.name
                description rootProject.description
                url 'https://www.github.com/blackducksoftware/integration-gradle-inspector'
                packaging 'jar'
                scm {
                    connection 'scm:git:git://github.com/blackducksoftware/integration-gradle-inspector.git'
                    developerConnection 'scm:git:git@github.com:blackducksoftware/integration-gradle-inspector.git'
                    url 'https://www.github.com/blackducksoftware/integration-gradle-inspector'
                }
                licenses {
                    license {
                        name 'Apache License 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        id 'blackduckoss'
                        name 'Black Duck OSS'
                        email 'bdsoss@blackducksoftware.com'
                        organization 'Black Duck Software, Inc.'
                        organizationUrl 'http://www.blackducksoftware.com'
                        roles { role 'developer' }
                        timezone 'America/New_York'
                    }
                }
            }
        }
    }
}

gradle.taskGraph.whenReady {
    if (gradle.taskGraph.hasTask(":uploadArchives")) {
        println "Uploading ${rootProject.name} version ${version} to maven central."
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task printVersion {
    doLast{
       println project.version
    }
}

artifacts {
    archives jar
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

dependencies {
    compile ('com.blackducksoftware.integration:hub-detect-model:0.0.2') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    compile 'com.blackducksoftware.integration:integration-bdio:4.2.1'
    compile 'com.google.code.gson:gson:2.7'
    compile gradleApi()
}
